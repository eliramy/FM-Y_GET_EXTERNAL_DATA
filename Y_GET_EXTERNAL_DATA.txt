FUNCTION y_get_external_data.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IV_PROG) TYPE  PROGNAME
*"     REFERENCE(IV_VARIANT) TYPE  VARIANT
*"  EXPORTING
*"     REFERENCE(EV_DATA) TYPE REF TO  DATA
*"     REFERENCE(ES_METADATA) TYPE
*"        CL_SALV_BS_RUNTIME_INFO=>S_TYPE_METADATA
*"----------------------------------------------------------------------

  cl_salv_bs_runtime_info=>set(
    EXPORTING
      display        = abap_false
      metadata       = abap_true
      data           = abap_true ).
*    structure      =     " Table Name
*    structure_line =     " Table Name

  SUBMIT (iv_prog) USING SELECTION-SET iv_variant AND RETURN.

  TRY.
      CALL METHOD cl_salv_bs_runtime_info=>get_data_ref
        IMPORTING
          r_data = ev_data.
*         r_data_line       =
*         r_data_descr      =
*         r_data_line_descr =

      es_metadata = cl_salv_bs_runtime_info=>get_metadata( ).

    CATCH cx_salv_bs_sc_runtime_info INTO DATA(lo_cx).

      DATA(lv_err) = lo_cx->if_message~get_longtext( ).

      IF lv_err IS INITIAL.
        lv_err = lo_cx->if_message~get_text( ) .
      ENDIF.

      MESSAGE lv_err TYPE 'E'.

  ENDTRY.

  cl_salv_bs_runtime_info=>clear_all( ).

ENDFUNCTION.
